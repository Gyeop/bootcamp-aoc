open Belt
let input = Node.Fs.readFileAsUtf8Sync("input/Week2/Year2020Day4.sample.txt")
let passportStrings = input->Js.String2.split("\n")->Js.Array2.joinWith(" ")->Js.String2.split("  ")

// --- Common Codes ----
type keys = Byr | Iyr | Eyr | Hgt | Hcl | Ecl | Pid | Cid
let keysToStringFromPassportField = key =>
  switch key {
  | Byr => "byr"
  | Iyr => "iyr"
  | Eyr => "eyr"
  | Hgt => "hgt"
  | Hcl => "hcl"
  | Ecl => "ecl"
  | Pid => "pid"
  | Cid => "cid"
  }

let createKeyValueTuple = str => {
  let field = str->Js.String2.split(":")
  switch (Belt.Array.get(field, 0), Belt.Array.get(field, 1)) {
  | (Some(key), Some(value)) => (key, value)
  | _ => ("", "")
  }
}

let parseToDict = passports =>
  passports
  ->Belt.Array.map(x => x->Js.String2.split(" "))
  ->Belt.Array.map(passport => passport->Belt.Array.map(createKeyValueTuple))
  ->Belt.Array.map(passport => passport->Js.Dict.fromArray)

let passportDicts = parseToDict(passportStrings)

// ----- Part 1 Codes -----
type passport = {
  byr: string,
  iyr: string,
  eyr: string,
  hgt: string,
  hcl: string,
  ecl: string,
  pid: string,
  cid: option<string>,
}

let checkValidPassport = (dict: Js.Dict.t<string>) => {
  let byr = dict->Js.Dict.get(keysToStringFromPassportField(Byr))
  let iyr = dict->Js.Dict.get(keysToStringFromPassportField(Iyr))
  let eyr = dict->Js.Dict.get(keysToStringFromPassportField(Eyr))
  let hgt = dict->Js.Dict.get(keysToStringFromPassportField(Hgt))
  let hcl = dict->Js.Dict.get(keysToStringFromPassportField(Hcl))
  let ecl = dict->Js.Dict.get(keysToStringFromPassportField(Ecl))
  let pid = dict->Js.Dict.get(keysToStringFromPassportField(Pid))
  let cid = dict->Js.Dict.get(keysToStringFromPassportField(Cid))

  switch (byr, iyr, eyr, hgt, hcl, ecl, pid, cid) {
  | (Some(byr), Some(iyr), Some(eyr), Some(hgt), Some(hcl), Some(ecl), Some(pid), _) =>
    Some({
      byr: byr,
      iyr: iyr,
      eyr: eyr,
      hgt: hgt,
      hcl: hcl,
      ecl: ecl,
      pid: pid,
      cid: cid,
    })
  | _ => None
  }
}

// Part1 Result
let validPassports = passportDicts->Belt.Array.map(checkValidPassport)->Belt.Array.keepMap(x => x)
let validPassportsCount = validPassports->Belt.Array.size
validPassportsCount->Js.log // 190

// ----- Part 2 -----
type eyeColor = Amb | Blu | Brn | Gry | Grn | Hzl | Oth
type height = Cm(int) | Inch(int)
let stringToKeyFromEyeColor = (key: string): option<eyeColor> =>
  switch key {
  | "amb" => Some(Amb)
  | "blu" => Some(Blu)
  | "brn" => Some(Brn)
  | "gry" => Some(Gry)
  | "grn" => Some(Grn)
  | "hzl" => Some(Hzl)
  | "oth" => Some(Oth)
  | _ => None
  }

type passport2 = {
  byr: int,
  iyr: int,
  eyr: int,
  hgt: height,
  hcl: string,
  ecl: eyeColor,
  pid: string,
  cid: option<string>,
}

let parseInt = (str: string) => Belt.Int.fromString(str)

// íŒŒì‹±ì€ íŒŒì‹±ë§Œ, ê²€ì¦ì€ ê²€ì¦ë§Œ ê°ê° í•¨ìˆ˜ë¥¼ ë¶„ë¦¬í•œë‹¤.
// íŒŒì‹± -> ë°ì´í„°ë¥¼ í˜•íƒœ(íƒ€ìž…)ì— ë§žê²Œ ë³€í™˜
// ê²€ì¦ -> ì¡°ê±´ì— ë”°ë¼ í•„í„°ë§(íŠ¹ì • ë²”ìœ„, íŠ¹ì • ë¬¸ìžì—´, íŠ¹ì •ê¸¸ì´ ë“±)

// ì²˜ìŒ ê°ê° íŒŒì‹±ê³¼ ê²€ì¦ì— ë”°ë¥¸ í•¨ìˆ˜ë¥¼ ë¶„ë¦¬í•´ì„œ ìž‘ì„±í–ˆëŠ”ë°,
// íŒŒì‹±ë‹¨ê³„ì—ì„œ íƒ€ìž…ì„ ë§žì¶°ì£¼ë©´ì„œ íŒ¨í„´ë§¤ì¹­ìœ¼ë¡œ ì¸í•´ ë°ì´í„°ê°€ ì—†ëŠ” ì—¬ê¶Œë“¤ì€ ì „ë¶€ Noneìœ¼ë¡œ ë³€í•´ë²„ë¦°ë‹¤.
// ê·¸ë ‡ê²Œë˜ë©´ í•´ë‹¹ íŒŒì‹±ë‹¨ê³„ì—ì„œ ì¼ë¶€ ì—¬ê¶Œì— ëŒ€í•œ ê²€ì¦ì´ í•œ ë²ˆì— ì²˜ë¦¬ëœ ê²ƒì´ ì•„ë‹Œê°€?

// ê·¸ëŸ¼ ê·¸ëƒ¥ í•©ì¹  ìˆ˜ ì—†ë‚˜? => í•©ì¹œ ê²°ê³¼, í•˜ë‚˜ë¥¼ "íŒŒì‹±->...->ê²€ì¦" ë‹¨ê³„ë¥¼ ê±°ì¹˜ë©° ê° í”„ë¡œí¼í‹°ì— ëŒ€í•œ íŒŒì´í”„ê°€ ê¸¸ì–´ì§
// ê·¸ë ‡ì§€ë§Œ ì¤‘ê°„ì— ê²€ì¦ë˜ì§€ ì•Šì€ ë°ì´í„°ëŠ” ì „í˜€ ì‚¬ìš©í•  í•„ìš”ê°€ ì—†ìœ¼ë¯€ë¡œ, í•œ ë²ˆì— ì´ ê³¼ì •ì„ ì „ì²˜ë¦¬í•´ì£¼ëŠ” ê²ƒì´ ë”ìš± ì´ë“ì´ í°ê°€?
// ë” ìœ ì—°í•˜ê²Œ í•¨ìˆ˜ë¥¼ ë‚˜ëˆ„ì–´ì„œ ì²˜ë¦¬í•˜ëŠ” ë°©ë²•? (íŒ¨í„´ë§¤ì¹­ì´ ê¸¸ì–´ì§€ì§€ ì•Šìœ¼ë©´ì„œë„.)

// ðŸš§ í”¼ë“œë°±ë°›ì€ ë¶€ë¶„
// ìœ„ì˜ ì§ˆë¬¸ì— ëŒ€í•œ ê³ ë¯¼ì´ ëœë‹¤ë©´, ì°¨ë¼ë¦¬ ë‹¨ê³„ë³„ë¡œ íŒŒì‹±í•  ìˆ˜ ìžˆëŠ” í•¨ìˆ˜ë¡œ ë¶„ë¦¬í•˜ìž.
// parse1 :: string => option<Js.Dict.t<string>>
// parse2 :: Js.Dict.t<string> => option<passport1>
// parse2 :: passport1 => passport2

let parsePassport = (dict: Js.Dict.t<string>) => {
  let byr = dict->Js.Dict.get(keysToStringFromPassportField(Byr))
  let iyr = dict->Js.Dict.get(keysToStringFromPassportField(Iyr))
  let eyr = dict->Js.Dict.get(keysToStringFromPassportField(Eyr))
  let hgt = dict->Js.Dict.get(keysToStringFromPassportField(Hgt))
  let hcl = dict->Js.Dict.get(keysToStringFromPassportField(Hcl))
  let ecl = dict->Js.Dict.get(keysToStringFromPassportField(Ecl))
  let pid = dict->Js.Dict.get(keysToStringFromPassportField(Pid))
  let cid = dict->Js.Dict.get(keysToStringFromPassportField(Cid))

  let checkRange = (~min: int, ~max: int, number: int): option<int> => {
    number >= min && max >= number ? Some(number) : None
  }

  let checkHeight = (height: height) => {
    switch height {
    | Cm(v) => v->checkRange(~min=150, ~max=193)->Belt.Option.map(x => Cm(x)) // ì¼ì •í•œ ìž…ì¶œë ¥ì„ mapìœ¼ë¡œ ì¤„ì¸ ì¼€ì´ìŠ¤
    | Inch(v) =>
      switch v->checkRange(~min=59, ~max=76) {
      | Some(v) => Some(Inch(v))
      | None => None // ìœ„ì˜ ì¼€ì´ìŠ¤ëŠ” ì´ëŸ° ë°©ì‹ê³¼ ê°™ë‹¤. ìž…ì¶œë ¥ì´ ì¼ì •(Some/None)í•˜ë‹¤ë©´ ì¤‘ì²©ë˜ëŠ” switchë¥¼ ì¤„ì—¬ë³´ìž.
      }
    }
  }

  let checkHairColor = hex => {
    let re = %re("/(#){1}([a-f]|[A-F]|[0-9]){6}/")
    Js.Re.test_(re, hex) ? Some(hex) : None
  }

  let checkPassportId = (id: string) => id->Js.String2.length === 9 ? Some(id) : None

  let parseHeight = (heightString: string): option<height> => {
    // regexë¡œ ì²˜ë¦¬í•˜ë ¤ë‹ˆ, nested optionì´ ë˜ì–´ ì²˜ë¦¬í•˜ê¸°ê°€ ë²ˆê±°ë¡œì›Œì§„ë‹¤.
    // let numberString = heightString->Js.String2.splitByRe(%re("/cm|in/"))->Belt.Array.get(0) // option<option<int>> ..
    // -> nested optionì„ ì œê±°í•˜ë ¤ë©´ ì½”ë“œê°€ ê¸¸ì–´ì§€ëŠ”ë°, ì´ê²Œ ì¢‹ì€ ì½”ë“œì¼ê¹Œ? ê³ ë¯¼ë˜ëŠ”ë¶€ë¶„.
    // ðŸš§ í”¼ë“œë°±ë°›ì€ ë¶€ë¶„
    // ì´ê±´ ì·¨í–¥ì— ê´€í•œ ë¶€ë¶„, ì–´ë–¤ê²Œ ë” ì¢‹ë‹¤ê³  ëª…í™•ížˆ íŒë‹¨í•  ìˆ˜ëŠ” ì—†ë‹¤. ì½”ë“œë¥¼ ë§Žì´ ì¨ë³´ê³ ,
    // ë‹¤ë¥¸ ì‚¬ëžŒë“¤ì˜ ì½”ë“œë„ ë§Žì´ ë³´ë©´ì„œ, ìƒí™©ì— ë§žëŠ” ì˜¬ë°”ë¥¸ ì„ íƒì„ í•˜ìž.
    // let numberString =
    //   heightString
    //   ->Js.String2.splitByRe(%re("/cm|in/"))
    //   ->Belt.Array.keepMap(x => x)
    //   ->Belt.Array.get(0)
    //   ->Belt.Option.flatMap(x => x->Belt.Int.fromString)

    // ì•„ëž˜ì²˜ëŸ¼ íŠ¹ì • indexë¡œ ìžë¥´ë©´, ë§Œì•½ ë‹¨ìœ„ê°€ "m" ì²˜ëŸ¼ í•œê¸€ìžê°€ ë“¤ì–´ì˜¤ê²Œë˜ë©´ ëŒ€ì‘í•  ìˆ˜ ì—†ì–´ì§„ë‹¤.
    let number = heightString->Js.String2.slice(~from=0, ~to_=-2)->Belt.Int.fromString
    let unit = heightString->Js.String2.sliceToEnd(~from=-2)

    // ì™€ì¼ë“œì¹´ë“œì— ì „ì ìœ¼ë¡œ ì˜ì¡´í•˜ì§€ ë§ìž
    // ì™€ì¼ë“œì¹´ë“œì— ì „ì ìœ¼ë¡œ ì˜ì¡´í•˜ê²Œë˜ë©´, ìƒˆë¡œìš´ ë°ì´í„°ê°€ ì¶”ê°€ë˜ëŠ” ê²½ìš°, ì „ë¶€ ì™€ì¼ë“œì¹´ë“œë¡œ í˜ë ¤ë²„ë¦¬ê¸° ë•Œë¬¸ì—, ì»´íŒŒì¼ ë‹¨ê³„ì—ì„œ ì˜¤ë¥˜ë‚˜ ë²„ê·¸ë¥¼ ì°¾ì•„ë‚´ê¸° ì–´ë µê²Œ ëœë‹¤.
    switch (number, unit) {
    | (Some(number), "cm") => Some(Cm(number))
    | (Some(number), "in") => Some(Inch(number))
    | (Some(_), _) | (None, _) => None
    }
  }

  // DRY. ì¶”ìƒí™”í•´ì„œ ì²˜ë¦¬í•˜ìž.
  let parseRange = (str, min, max) =>
    str
    ->Belt.Option.flatMap(x => x->Belt.Int.fromString)
    ->Belt.Option.flatMap(x => x->checkRange(~min, ~max))

  let validByr = byr->parseRange(1920, 2002)
  let validIyr = iyr->parseRange(2010, 2020)
  let validEyr = eyr->parseRange(2020, 2030)

  let validHgt =
    hgt->Belt.Option.flatMap(x => x->parseHeight)->Belt.Option.flatMap(x => x->checkHeight)

  let validHcl = hcl->Belt.Option.flatMap(x => x->checkHairColor)

  let validPid = pid->Belt.Option.flatMap(x => x->checkPassportId)

  let validEcl = ecl->Belt.Option.flatMap(x => x->stringToKeyFromEyeColor)

  switch (validByr, validIyr, validEyr, validHgt, validHcl, validEcl, validPid, cid) {
  | (
      Some(validByr),
      Some(validIyr),
      Some(validEyr),
      Some(validHgt),
      Some(validHcl),
      Some(validEcl),
      Some(validPid),
      cid,
    ) =>
    Some({
      byr: validByr,
      iyr: validIyr,
      eyr: validEyr,
      hgt: validHgt,
      hcl: validHcl,
      ecl: validEcl,
      pid: validPid,
      cid: cid,
    })
  | _ => None
  }
}

// Part2 Result
let validPassports2 = passportDicts->Belt.Array.map(parsePassport)
validPassports2->Belt.Array.keepMap(x => x)->Js.Array.length->Js.log // 121
